     A                                      DSPSIZ(24 80 *DS3)
     A                                      CHGINPDFT(HI UL)
     A                                      CSRINPONLY
     A                                      ERRSFL
     A                                      CF03(03 'Exit')
     A                                      CF05(05 'Refresh')
     A                                      CF10(10 'Run')
     A                                      CF11(11 'Toggle field')
     A                                      CF12(12 'Exit')
     A                                      MOUBTN(*ULD CF11)
     A          R SFLA                      SFL
     A            COL1O          1A  B 11  4SFLCSRPRG
     A  31                                  DSPATR(PR)
     A N31                                  DSPATR(UL)
     A            COL1          20A  O 11  6DSPATR(HI)
     A            COL2O          1A  B 11 29SFLCSRPRG
     A  32                                  DSPATR(PR)
     A N32                                  DSPATR(UL)
     A            COL2          20A  O 11 31DSPATR(HI)
     A            COL3O          1A  B 11 54SFLCSRPRG
     A  33                                  DSPATR(PR)
     A N33                                  DSPATR(UL)
     A            COL3          20A  O 11 56DSPATR(HI)
     A            COL1H          2A  H
     A            COL2H          2A  H
     A            COL3H          2A  H
     A          R FMT01                     SFLCTL(SFLA)
     A                                      SFLPAG(0011)
     A                                      SFLSIZ(&NBRREC)
     A                                      OVERLAY
     A                                      RTNCSRLOC(&REC &FLD)
     A                                      SFLCSRRRN(&RELRCD)
     A  90                                  SFLDSP
     A  90                                  SFLDSPCTL
     A N90                                  SFLCLR
     A  91                                  SFLEND(*SCRBAR *MORE)
     A            RELRCD         5S 0H
     A            NBRREC         5S 0P
     A            REC           10A  H
     A            FLD           10A  H
     A                                  1 69DATE
     A                                      EDTCDE(Y)
     A                                  2 69TIME
     A                                  3 69SYSNAME
     A                                  4 69'Filter'
     A                                  5 69USER
     A                                  7  4'Filter by these fields'
     A                                      DSPATR(HI)
     A                                 10  4'  Type                '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 10 29'  State               '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                 10 54'  Country             '
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A                                  7 49'Select for printing = "Y"'
     A                                  8 49'Print report = Run, or F10'
     A                                      DSPATR(BL)
     A                                      COLOR(GRN)
     A*                                 1  3'   ____    _____     __           -
     A*                                            ____     __   ____ __   '
     A*                                 2  3'  |_  /___/ ___/__  / /_ ____ _  _-
     A*                                     __    / __/_ __/ /  / _(_) /__ '
     A*                                 3  3' _/_ <___/ /__/ _ \/ / // /    \/ -
     A*                                     _ \  _\ \/ // / _ \/ _/ / / -_)'
     A*                                 4  3'/____/   \___/\___/_/\_,_/_/_/_/_/-
     A*                                     /_/ /___/\_,_/_.__/_//_/_/\__/ '
     A          R FOOTER
     A            PB1            2Y 0B 23  3PSHBTNFLD
     A                                      PSHBTNCHC(1 'Re>fresh (F5) ' CF05)
     A                                      PSHBTNCHC(2 '>Run (F10) ' CF10)
     A                                      PSHBTNCHC(3 '>Cancel (F12) ' CF12)
	
