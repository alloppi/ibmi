/*   CL PROGRAM TO BUILD THE ACME ORDERING SYSTEM              +
     TO COMPILE:                                               +
        CRTBNDCL BUILDIT SRCFILE(XXX/QCLSRC) DBGVIEW(*LIST)    */
PGM

    /********************************************************+
     * THE FOLLOWING FOR CUSTOMER MAINTENANCE                +
     ********************************************************/

     DLTF       FILE(*CURLIB/CUSTF)
     MONMSG     CPF2105

     CRTPF FILE(CUSTF) SRCFILE(*CURLIB/QRPGLESRC)

     RUNSQLSTM  SRCFILE(QRPGLESRC) SRCMBR(DEMOCUS) +
                COMMIT(*NONE) NAMING(*SYS)
     DLTSPLF    FILE(DEMOCUS) SPLNBR(*LAST)

     CRTRPGPGM  PGM(CUSTMNT) SRCFILE(*CURLIB/QRPGLESRC) OPTION(*LSTDBG)

     CRTRPGMOD  MODULE(QTEMP/CUSTR4) SRCFILE(*CURLIB/QRPGLESRC) DBGVIEW(*LIST)
     CRTSRVPGM  CUSTR4 EXPORT(*SRCFILE) SRCFILE(*CURLIB/QSRVSRC)

     CRTDSPF    FILE(*CURLIB/CUSTS) SRCFILE(*CURLIB/QRPGLESRC)
     CRTRPGMOD  MODULE(QTEMP/CUSTVIEWR4) SRCFILE(*CURLIB/QRPGLESRC) DBGVIEW(*LIST)

     CRTRPGMOD QTEMP/CUSTMNTR4 SRCFILE(*CURLIB/QRPGLESRC) DBGVIEW(*LIST)
     CRTPGM    PGM(*CURLIB/CUSTMNTR4) MODULE(QTEMP/CUSTMNTR4 QTEMP/CUSTVIEWR4) BNDSRVPGM((CUSTR4))


    /********************************************************+
     * DELETE DEMO FILES IF THEY EXIST                       +
     ********************************************************/

             DLTF       FILE(*CURLIB/CUSTFILE)
             MONMSG     CPF2105
             DLTF       FILE(*CURLIB/ITEMFILE)
             MONMSG     CPF2105
             DLTF       FILE(*CURLIB/ORDHEAD)
             MONMSG     CPF2105
             DLTF       FILE(*CURLIB/ORDITEM)
             MONMSG     CPF2105
             DLTF       FILE(*CURLIB/CTRLFILE)
             MONMSG     CPF2105
             DLTF       FILE(*CURLIB/CUSTFILEF)
             MONMSG     CPF2105
             DLTF       FILE(*CURLIB/ITEMFILEF)
             MONMSG     CPF2105
             DLTF       FILE(*CURLIB/ORDHEADF)
             MONMSG     CPF2105
             DLTF       FILE(*CURLIB/ORDITEMF)
             MONMSG     CPF2105
             DLTF       FILE(*CURLIB/CTRLFILEF)
             MONMSG     CPF2105
             DLTF       FILE(*CURLIB/ACMEORDS)
             MONMSG     CPF2105

    /********************************************************+
     * BUILD NEW FILES AND RENAME THEM:                      +
     *   -- SQL MAKES RECFMT NAMES SAME AS FILENAMES.        +
     *   -- SO WE BUILD THE FILE WITH THE INTENDED RECFMT    +
     *   -- THEN RENAME TO GET THE PROPER FILE NAME          +
     *                                                       +
     *  NOTE: IN V5R4, THERE'S A RECFMT KEYWORD TO THE       +
     *        CREATE TABLE STATEMENT SO THIS ISN'T NEEDED    +
     *        BUT THIS CODE STRIVES TO BE V5R3 COMPATIBLE    +
     ********************************************************/

     RUNSQLSTM  SRCFILE(QRPGLESRC) SRCMBR(MKFILESSQL) +
                COMMIT(*NONE) NAMING(*SYS)
     DLTSPLF    FILE(MKFILESSQL) SPLNBR(*LAST)

     RNMOBJ OBJ(*CURLIB/CUSTFILEF) OBJTYPE(*FILE) NEWOBJ(CUSTFILE)
     RNMOBJ OBJ(*CURLIB/ITEMFILEF) OBJTYPE(*FILE) NEWOBJ(ITEMFILE)
     RNMOBJ OBJ(*CURLIB/ORDHEADF)  OBJTYPE(*FILE) NEWOBJ(ORDHEAD)
     RNMOBJ OBJ(*CURLIB/ORDITEMF)  OBJTYPE(*FILE) NEWOBJ(ORDITEM)
     RNMOBJ OBJ(*CURLIB/CTRLFILEF) OBJTYPE(*FILE) NEWOBJ(CTRLFILE)

     RUNSQLSTM  SRCFILE(QRPGLESRC) SRCMBR(DEMORECS) +
                COMMIT(*NONE) NAMING(*SYS)
     DLTSPLF    FILE(DEMORECS) SPLNBR(*LAST)

     /******************************************************+
     * BUILD THE SERVICE PROGRAM THAT CONTAINS THE BUSINESS +
     * LOGIC FOR THIS PROJECT                               +
     ********************************************************/

     CRTSQLRPGI OBJ(QTEMP/ORDERR4) +
                OBJTYPE(*MODULE) +
                SRCFILE(*LIBL/QRPGLESRC) +
                DBGVIEW(*SOURCE)

     CRTSRVPGM SRVPGM(*CURLIB/ORDERR4) +
               MODULE(QTEMP/ORDERR4) +
               EXPORT(*SRCFILE) +
               SRCFILE(*LIBL/QSRVSRC) +
               ACTGRP(*CALLER)

     DLTMOD MODULE(QTEMP/ORDERR4)

     DLTSPLF    FILE(ORDERR4) JOB(*) SPLNBR(*LAST)
     MONMSG     MSGID(CPF3303)

     /******************************************************+
     * CREATE STORED PROCEDURE INTERFACES FOR ORDERR4       +
     ********************************************************/

     RUNSQLSTM  SRCFILE(QRPGLESRC) SRCMBR(CRTPROC) +
                COMMIT(*NONE) NAMING(*SYS) ERRLVL(20)
     DLTSPLF    FILE(CRTPROC) SPLNBR(*LAST)


     /******************************************************+
     * BUILD THE 5250 INTERFACE FOR PLACING ORDERS          +
     ********************************************************/

     CRTDSPF    FILE(ACMEORDS) SRCFILE(*LIBL/QRPGLESRC)
     CRTRPGMOD  MODULE(QTEMP/ACMEORDR4) SRCFILE(*LIBL/QRPGLESRC) +
                  DBGVIEW(*LIST)
     CRTPGM     PGM(ACMEORDR4) MODULE(QTEMP/ACMEORDR4) BNDSRVPGM(ORDERR4) +
                  ACTGRP(ACME)
     DLTSPLF    FILE(ACMEORDS) JOB(*) SPLNBR(*LAST)
     DLTSPLF    FILE(ACMEORDR4) JOB(*) SPLNBR(*LAST)

ENDPGM
