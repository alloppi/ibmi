     A                                      CF03(03)
      * Command key prompts
     A          R FMT1
     A                                 23  4'F3=Exit'
      * Prompt for Order Number
     A          R PROMPT
     A                                  3  2'Order Number . . . . . .'
     A            ORDNO     R        I  3 28REFFLD(ORDREC/ORDNO ORDER)
     A  50                             22  2'Incorrect Order Number'
     A                                 23  4'F3=Exit'
      * Subfile for parts ordered
     A          R SFLRCD                    SFL
     A            PARTNO    R        O 12  4REFFLD(ORDDEC/PARTNO ORDDET)
     A                                      EDTCDE(4)
     A            PARTDESC  R        O 12 12REFFLD(INVREC/PARTDESC INVEN)
     A            ORDERQTY  R        O 12 65REFFLD(ORDDEC/ORDERQTY ORDDET)
     A                                      EDTCDE(1)
      * Subfile control and main display
     A          R SFLCTL                    SFLCTL(SFLRCD)
     A                                      SFLSIZ(100)
     A                                      SFLPAG(9)
     A N25                                  SFLDSPCTL
     A                                      OVERLAY
     A  21                                  SFLDSP
     A  25                                  SFLCLR
     A                                  1 28'Ship To Information'
     A                                  3  2'Company . . . . . . . .'
     A            COMPANY   R        O  3 28REFFLD(ORDREC/COMPANY ORDER)
     A                                  4  2'Contact . . . . . . . .'
     A            CONTACT   R        O  4 28REFFLD(ORDREC/CONTACT ORDER)
     A                                  6  2'Status  . . . . . . . .'
     A            ORDSTS    R        O  6 28REFFLD(ORDREC/ORDSTS ORDER)
     A                                  8  2'Ship to address . . . .'
     A            ADDR1     R        O  8 28REFFLD(ORDREC/ADDR1 ORDER)
     A            ADDR2     R        O  9 28REFFLD(ORDREC/ADDR2 ORDER)
     A                                 11  4'Part No'
     A                                 11 12'Part Description'
     A                                 11 65'Quantity'
